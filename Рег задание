Чтобы не выполнять обмены в тестовых базах нужно
1. Добавить строчкИ в РегламентныеЗаданияПереопределяемый.ПриОпределенииНастроекРегламентныхЗаданий()
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ИМЯ_РЕГ_ЗАДАНИЯ;
	Настройка.РаботаетСВнешнимиРесурсами = Истина;
2. В самом начале процедуры, которая запускает рег.задание надо добавить строчку
ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(МетаЗадания);


// !+ ИТПл Григорьев_СВ 2025-05-27 #4662
Процедура ИТПлЗакрытьЗаказыКлиента() Экспорт

	МетаЗадание = Метаданные.РегламентныеЗадания.ИТПлЗакрытиеЗаказовКлиента;
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(МетаЗадание);
	
	ЗаписьЖурналаРегистрации(МетаЗадание.Синоним,
		УровеньЖурналаРегистрации.Информация,
		МетаЗадание,
		,
		НСтр("ru = 'Запуск задания'"));
	
	ИмяМетода = МетаЗадание.ИмяМетода;
	
	Отбор = Новый Структура();
	Отбор.Вставить("ИмяМетода", ИмяМетода);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	АктивныеФоновыеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
	Если АктивныеФоновыеЗадания.Количество() > 1 Тогда
		
		ТекстСообщения = НСтр("ru = 'Отмена задания по причине: задание уже выполняется.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
		ЗаписьЖурналаРегистрации(МетаЗадание.Синоним,
			УровеньЖурналаРегистрации.Информация,
			МетаЗадание,
			,
			ТекстСообщения);
		
		Возврат;
		
	КонецЕсли;
	
	ТекстСообщения = СтрШаблон("Закрытие проведененных заказов клиента в статусе ""Отгружен""");
	ЗаписьЖурналаРегистрации(МетаЗадание.Синоним,
		УровеньЖурналаРегистрации.Информация,
		МетаЗадание,
		,
		ТекстСообщения);
	
	МассивОшибок = Новый Массив;
	
	ЗакрытьЗаказы(МассивОшибок);
	
	ТекстСообщения = СтрШаблон("Завершение задания %1", СтрСоединить(МассивОшибок,";"));
	ЗаписьЖурналаРегистрации(МетаЗадание.Синоним,
		УровеньЖурналаРегистрации.Информация,
		МетаЗадание,
		,
		ТекстСообщения);

КонецПроцедуры // !- ИТПл Григорьев_СВ 2025-05-27
