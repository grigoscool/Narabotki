1.Добавить реквизит на форму программно

Если Форма.ИмяФормы = "Справочник.БанковскиеСчетаОрганизаций.Форма.ФормаЭлемента" Тогда
		
		ИмяЭлемента = "ДЕФА_ТекстНазначенияПлатежа";
		
		НовыйЭлемент = Форма.Элементы.Вставить(ИмяЭлемента, Тип("ПолеФормы"), Форма.Элементы.СтраницаНастройкаПечати, Форма.Элементы.ИспользоватьТекстКорреспондента);
		НовыйЭлемент.ПутьКДанным			= "Объект." + ИмяЭлемента;
		НовыйЭлемент.Вид					= ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.АвтоМаксимальнаяШирина = Ложь;
		НовыйЭлемент.МногострочныйРежим 	= Истина;
		
	КонецЕсли;


2.Создать произвольную таблицу значений на форме

&НаСервере
Процедура СоздатьТаблицуФормы(Знач ИмяПоляТаблицыФормы, Знач ИмяРеквизитаДанныеФормыКоллекция, Знач ТаблицаЗначений)
	
	// Если руками не создали эелемент формы Таблица, то создается программно
	Если Элементы.Найти(ИмяПоляТаблицыФормы) = Неопределено Тогда
		ЭлементРеквизита = Элементы.Добавить(ИмяПоляТаблицыФормы, Тип("ТаблицаФормы"),);
		ЭлементРеквизита.ПутьКДанным = ИмяРеквизитаДанныеФормыКоллекция;
	КонецЕсли;
	
	УдаляемыеРеквизиты = Новый Массив;
	РеквизитыДляУдаления = ПолучитьРеквизиты(ИмяРеквизитаДанныеФормыКоллекция);
	Для Каждого РеквизитУдаления Из РеквизитыДляУдаления Цикл
		УдаляемыеРеквизиты.Добавить(ИмяРеквизитаДанныеФормыКоллекция+"."+РеквизитУдаления.Имя);
		// Удаляем элементы формы
		Элементы.Удалить(Элементы[ИмяПоляТаблицыФормы+РеквизитУдаления.Имя]);
	КонецЦикла;
	
	// Добавление реквизитов в таблицу формы
	ДобавляемыеРеквизиты = Новый Массив;
	Для каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(Колонка.Имя, Колонка.ТипЗначения, ИмяРеквизитаДанныеФормыКоллекция, Колонка.Заголовок));
	КонецЦикла;
	ИзменитьРеквизиты(ДобавляемыеРеквизиты,УдаляемыеРеквизиты);
	
	
	// Добавление элементов на форму
	Для каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		ЭлементРеквизита = Элементы.Добавить(ИмяПоляТаблицыФормы + Колонка.Имя, Тип("ПолеФормы"), Элементы[ИмяПоляТаблицыФормы]);
		ЭлементРеквизита.ПутьКДанным = ИмяРеквизитаДанныеФормыКоллекция + "." + Колонка.Имя;
		ЭлементРеквизита.Вид = ВидПоляФормы.ПолеВвода;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ТаблицаЗначений, ИмяРеквизитаДанныеФормыКоллекция);
	
КонецПроцедуры

ТЗ = СоздатьКолонкиТЗ(РезультатЗапроса);

&НаСервере
Функция СоздатьКолонкиТЗ (РезультатЗапроса)
	
	ТЗ = Новый ТаблицаЗначений;
	Для Каждого ТекКолонка Из РезультатЗапроса.Колонки Цикл
		ТЗ.Колонки.Добавить(ТекКолонка.Имя);	        
	КонецЦикла;
	
	Возврат ТЗ;	
	
КонецФункции

ПроизвестиОбработкуПолейВыборки(ВыборкаДетальныеЗаписи,ТЗ);

&НаСервере
Функция ПроизвестиОбработкуПолейВыборки(ВыборкаДетальныеЗаписи, Знач ТЗ)

	Если ТипЗнч(ТЗ) = Тип("ТаблицаЗначений") Тогда
		ТекСтрока = ТЗ.Добавить();
		
	Иначе    //если не ТаблицаЗначений, то ДеревоЗначений или строка дерева значений  
		
		ТекСтрока = ТЗ.Строки.Добавить();    
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ТекСтрока,ВыборкаДетальныеЗаписи); 
	
	Возврат ТекСтрока; 
	
КонецФункции 


3. Открыть форму с динамическим списком с параметром в запросе

- команда открывает форму и добавляет в структуру формы Параметры новое значение из параметров команды  

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	//Вставить содержимое обработчика.
	ПараметрыФормы = Новый Структура("Контрагент", ПараметрКоманды);
	ОткрытьФорму("Справочник.Контрагенты.Форма.ДвиженияБУ", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
КонецПроцедуры

- При создании формы на сервере обращаемся к структуре формы Параметы и передаем значение в параметр запроса динамического списка

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)                        
	Контрагент = Неопределено;
	Параметры.Свойство("Контрагент", Контрагент);
	СписокДвижений.Параметры.УстановитьЗначениеПараметра("Контрагент", Контрагент);
КонецПроцедуры

4. Оповещение пользователя 
4.1 Отправка письма на почту

ТипыКИ = Новый Массив;
ТипыКИ.Добавить(Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);

Объекты = Новый Массив();
ДиректорМТО = Константы.Расш1_ДиректорМТО.Получить();

Если Не ЗначениеЗаполнено(ДиректорМТО) Тогда
    
    Сообщить("Не удалось сформировать письмо об изменении статуса заказа клиента! Установите значение константы ""Директор МТО""!");
    Возврат;	

КонецЕсли;

Объекты.Добавить(Константы.Расш1_ДиректорМТО.Получить());
АдресаПолучателяМассив = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(Объекты, ТипыКИ,,ТекущаяДатаСеанса());    

Если Не АдресаПолучателяМассив.Количество() Тогда

    Сообщить("Не удалось сформировать письмо об изменении статуса заказа клиента! Не удалось получить данные по почте Директора МТО!");
    Возврат;

КонецЕсли;

АдресаПолучателя = АдресаПолучателяМассив[0].Представление; 

ТекстПисьма = "Автоинформирование об изменении статуса заказа клиента. Необходимо выполнить дальнейшие действия по бизнес-процессу.";

ШаблонСтрокДанных = Символы.ПС + "Клиент: %1
|Контрагент: %2
|Заказ клиента: %3
|Сделка: %4
|Новый статус заказа: %5
|Приоритет: %6";

СтрокиДанных = СтрШаблон(ШаблонСтрокДанных, Партнер,Контрагент,Ссылка,Сделка,"К выполнению",Приоритет); 
ТекстПисьма = ТекстПисьма + СтрокиДанных; 


ПараметрыСообщения = Новый Структура;
ПараметрыСообщения.Вставить("Кому",     АдресаПолучателя);
ПараметрыСообщения.Вставить("Тема",     "Изменении статуса заказа клиента");
ПараметрыСообщения.Вставить("Тело",     ТекстПисьма);
ПараметрыСообщения.Вставить("Вложения", Новый Массив); 

СистемнаяУчётнаяЗапись = Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты;

Попытка
    
    НовоеПисьмо = РаботаСПочтовымиСообщениями.ПодготовитьПисьмо(СистемнаяУчётнаяЗапись,ПараметрыСообщения);
    РаботаСПочтовымиСообщениями.ОтправитьПисьмо(СистемнаяУчётнаяЗапись, НовоеПисьмо);
    Сообщить("Было сформировано письмо об изменении статуса заказа клиента.");
    
Исключение
     Сообщить("Не удалось сформировать письмо об изменении статуса заказа клиента!"); 
КонецПопытки;
////////////////////////////////////////////////////////////////////////////////
ПараметрыПисьма = Новый Структура;
ПараметрыПисьма.Вставить("Кому", АдресаКоторымОтправляемПисьма);
ПараметрыПисьма.Вставить("УчетнаяЗапись", ПочтаОтправителя);                                                               
ПараметрыПисьма.Вставить("АдресОтвета", АдресаКоторымОтправляемПисьма);
ПараметрыПисьма.Вставить("Тема", ТемаПисьма);	
ПараметрыПисьма.Вставить("Тело", ТекстПисьма);
ПараметрыПисьма.Вставить("ТипТекста", "ПростойТекст");
РаботаСПочтовымиСообщениями.ОтправитьПочтовоеСообщение(ПочтаОтправителя, ПараметрыПисьма);

4.2 В центр оповещений
ПоказатьОповещениеПользователя("Данные заполнены", ПолучитьНавигационнуюСсылку(ПоследняяДобавленнаяСсылка), 
	"Открыть последний добавленный товар", БиблиотекаКартинок.Справочник , СтатусОповещенияПользователя.Важное);
