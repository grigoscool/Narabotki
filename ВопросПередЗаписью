!!!!!!!!!!!!!!!! ВАЖНО иметь глоб переменную "ПараметрыДляЗаписи" в модуле формы и процедуру "Подключаемый_ОбработатьЗаписьОбъекта" и реквизит формы "НеВыполнятьПроверкуПередЗаписью", "ПринудительноЗакрытьФорму"
&НаКлиенте
Перем ПараметрыДляЗаписи Экспорт;

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если ЭтаФорма.НеВыполнятьПроверкуПередЗаписью Тогда
		ЭтаФорма.НеВыполнятьПроверкуПередЗаписью = Ложь;
		Возврат;
	КонецЕсли;

	Если Объект.Заказы.Количество() > 0 Тогда
		РасхожденияЗаказомГрузоотправителя = ЕстьРасхожденияЗаказомГрузоотправителя();
		Если РасхожденияЗаказомГрузоотправителя.ЕстьРасхождения Тогда
		
			Отказ = Истина;
			
			Дополнительныепараметры = Новый Структура;
			Дополнительныепараметры.Вставить("ПараметрыЗаписи", ПараметрыЗаписи);
			Дополнительныепараметры.Вставить("ДатаОтправлениеС", РасхожденияЗаказомГрузоотправителя.ДатаОтправлениеС);
			
			Оповещение = Новый ОписаниеОповещения("ПередЗаписьюЗавершение", ЭтотОбъект, Дополнительныепараметры);
			ПоказатьВопрос(Оповещение, РасхожденияЗаказомГрузоотправителя.ТекстСообщения, РежимДиалогаВопрос.ДаНет, 0);
			
		КонецЕсли;
	КонецЕсли;
	//!- ИТПл Григорьев_СВ 2024-01-22
КонецПроцедуры 

&НаСервере
Функция ЕстьРасхожденияЗаказомГрузоотправителя()
	
	ЕстьРасхождения = Ложь;
	ДатаОтправлениеС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Заказы[0].ЗаказГрузоотправителя, "ОтправлениеС"); 
	
	Если Объект.ИТПлДатаОтправки <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Заказы[0].ЗаказГрузоотправителя, "ОтправлениеС") Тогда
	
		ТекстСообщения = Нстр("ru = 'Значение даты отправки в шапке док-та не совпадает с значениями, указанными в табличной части «Маршруты». Перезаполнить «Дату отправки» указанную в шапке док-та ?'");
		ЕстьРасхождения = Истина;
		
		Возврат Новый Структура("ЕстьРасхождения,ТекстСообщения,ДатаОтправлениеС", ЕстьРасхождения, ТекстСообщения, ДатаОтправлениеС);
		
	КонецЕсли;
	
	Возврат Новый Структура("ЕстьРасхождения", ЕстьРасхождения);

КонецФункции //!- ИТПл Григорьев_СВ 2024-01-22

&НаКлиенте
Процедура ПередЗаписьюЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.ИТПлДатаОтправки = ДополнительныеПараметры.ДатаОтправлениеС;	
	КонецЕсли;      
	
	ОбщегоНазначенияУТКлиент.ЗаписатьОбъектПриНеобходимости(ЭтотОбъект, ДополнительныеПараметры.ПараметрыЗаписи,,Истина);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьЗаписьОбъекта()
	
	ОбщегоНазначенияУТКлиент.ОбработатьЗаписьОбъектаВФорме(ЭтотОбъект, ПараметрыДляЗаписи);
	
КонецПроцедуры 

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	//!+ ИТПл Григорьев_СВ 2024-01-22 (RM #2382. ЧТЗ 07-03)
	ПринудительноЗакрытьФорму = Истина;
	//!- ИТПл Григорьев_СВ 2024-01-22 
КонецПроцедуры


---------------------
ОбщегоНазначенияУТКлиент

Процедура ЗаписатьОбъектПриНеобходимости(Форма, ПараметрыЗаписи, Отказ = Ложь, ПроверкиПройдены = Ложь) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Форма.ПараметрыДляЗаписи   = ПараметрыЗаписи;
	НеобходимаОтложеннаяЗапись = ПараметрыЗаписи.Свойство("ДействиеПослеЗаписи");
	Если НеобходимаОтложеннаяЗапись Или ПроверкиПройдены Тогда
		Отказ = Истина;
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ОбработатьЗаписьОбъекта", 0.1, Истина);
	Иначе
		НачатьЗамерВремениЗаписиОбъекта(Форма, ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
// 	Форма - РасширениеУправляемойФормыДляОбъектов - Форма документа:
// 		* Объект - ДокументОбъект - 
// 	ПараметрыЗаписи - Структура - 
// 	Отказ - Булево - Описание
// Возвращаемое значение:
// 	Булево - Описание
Функция ОбработатьЗаписьОбъектаВФорме(Форма, ПараметрыЗаписи, Отказ = Ложь) Экспорт
	
	ДействиеПослеЗаписи = Неопределено;
	Если ПараметрыЗаписи.Свойство("ДействиеПослеЗаписи", ДействиеПослеЗаписи) Тогда
		ПараметрыЗаписи.Удалить("ДействиеПослеЗаписи");
	КонецЕсли;
	
	Результат = Ложь;
	
	Если Отказ Тогда
		Возврат Результат;
	КонецЕсли;
	
	НачатьЗамерВремениЗаписиОбъекта(Форма, ПараметрыЗаписи);
	Форма.НеВыполнятьПроверкуПередЗаписью = Истина;
	
	Отказ = Истина;
	ПараметрыЗаписи.Вставить("ПринудительноЗакрытьФорму", Форма.ПринудительноЗакрытьФорму);
	ПараметрыЗаписи.Вставить("НовыйОбъект", Не ЗначениеЗаполнено(Форма.Объект.Ссылка));
	Результат = Форма.Записать(ПараметрыЗаписи);
	
	Форма.ПринудительноЗакрытьФорму = Ложь;
	Форма.НеВыполнятьПроверкуПередЗаписью = Ложь;
	
	Если Результат И ДействиеПослеЗаписи <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДействиеПослеЗаписи, Результат);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

